ext {
	GROUPID = "com.badlogicgames.gdx"
	VERSION = "1.7.1-SNAPSHOT"
	gdxVersion = "1.7.0"
	lwjglVersion = "2.9.2"
	lwjgl3Version = "3.0.0b-SNAPSHOT"
	jlayerVersion = "1.0.1-gdx"
	jorbisVersion = "0.0.17"
	junitVersion = "4.11"
}

apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "java"
apply plugin: 'maven'

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

group = GROUPID
version = VERSION

// set source Java version
sourceCompatibility = 1.6
targetCompatibility = 1.6
sourceSets.main.java.srcDirs = ["src"]
sourceSets.main.resources.srcDirs = ["src"]

dependencies {
	compile "com.badlogicgames.gdx:gdx:$gdxVersion"
	compile "org.lwjgl:lwjgl:$lwjgl3Version"
	compile "org.lwjgl:lwjgl-platform:$lwjgl3Version:natives-windows"
	compile "org.lwjgl:lwjgl-platform:$lwjgl3Version:natives-linux"
	compile "org.lwjgl:lwjgl-platform:$lwjgl3Version:natives-osx"
	compile "com.badlogicgames.jlayer:jlayer:$jlayerVersion"
	compile "org.jcraft:jorbis:$jorbisVersion"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

if (JavaVersion.current().isJava8Compatible()) {
	allprojects {
		tasks.withType(Javadoc) {
			options.addStringOption("Xdoclint:none", "-quiet")
		}
	}
}
